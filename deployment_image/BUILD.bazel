load("@container_structure_test//:defs.bzl", "container_structure_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@rules_pkg//:pkg.bzl", "pkg_tar")

# Combine all the binaries into a tar
pkg_tar(
    name = "binaries_tar",
    files = {
        "@bazelisk_binary//file": "bazel",
        "@terraform_binary//:file": "terraform",
    },
    mode = "0755",
    package_dir = "bin",
)

# Define the OCI image rule
oci_image(
    name = "deployment_image",
    base = "@debian",
    entrypoint = ["/entrypoint.sh"],
    env = {"PATH": "/usr/bin:/usr/local/bin"},
    tars = [
        ":binaries_tar",
        ":entrypoint_tar",
        ":extract_and_tar_ca_certificates",
    ],
)

oci_tarball(
    name = "deployment_image_tarball",
    image = ":deployment_image",
    repo_tags = ["deployment_image:test"],
)

container_structure_test(
    name = "deployment_image_test",
    configs = ["test.yaml"],
    image = ":deployment_image",
)

pkg_tar(
    name = "entrypoint_tar",
    srcs = ["entrypoint.sh"],
    mode = "0755",  # Give it execute permissions
    package_dir = "/",  # Place it at the root of the image
)

genrule(
    name = "extract_and_tar_ca_certificates",
    srcs = ["@ca-certificates-deb//file"],
    outs = ["ca-certificates-out.tar"],
    cmd = """
        mkdir -p temp_output
        dpkg -x $(location @ca-certificates-deb//file) temp_output
        tar -cf $@ -C temp_output/usr/share/ca-certificates .
    """,
)
